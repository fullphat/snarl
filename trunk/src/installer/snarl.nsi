; Script generated by the HM NIS Edit Script Wizard.

;  Var ALREADY_INSTALLED

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Snarl"
!define PRODUCT_VERSION "2.5 Beta 1"
!define PRODUCT_VERSION_EXE "2.5-beta1"
!define PRODUCT_PUBLISHER "full phat products"
!define PRODUCT_WEB_SITE "http://www.fullphat.net"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Snarl.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define CSIDL_PROGRAMS "0x02"
!define CSIDL_APPDATA "0x1A"

; /* Melon defines */

!define MELON_NEEDED "3.74"			; version number required
!define MELON_VER_REQ "0x00030704"		; version number required (hex mmmmnnss format)


; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "UAC.nsh"
!include "..\include\melon.nsh"
  !include Library.nsh
  !define LIBRARY_COM

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "in.ico"
!define MUI_UNICON "un.ico"

!define MUI_WELCOMEFINISHPAGE_BITMAP left.bmp


; ----- Pages -----

!insertmacro MUI_PAGE_WELCOME

!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "sbsd.rtf"

!define MUI_PAGE_CUSTOMFUNCTION_PRE checkforsnarl
;!define MUI_PAGE_CUSTOMFUNCTION_PRE checkformelon
;!insertmacro MUI_PAGE_COMPONENTS

!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN
;!define MUI_FINISHPAGE_RUN_TEXT "Open installation folder"
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchSnarl"
!define MUI_FINISHPAGE_SHOWREADME "http://sourceforge.net/apps/mediawiki/snarlwin/index.php?title=New_Features"
!define MUI_FINISHPAGE_NOAUTOCLOSE
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

RequestExecutionLevel user

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${PRODUCT_NAME}-${PRODUCT_VERSION_EXE}-setup.exe"
InstallDir "$PROGRAMFILES\full phat\Snarl"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section
SectionIn RO

;  RMDir /r "$INSTDIR\styles"		; fix for 1.0 installer
  RMDir /r "$INSTDIR\libs"		; ditto
  RMDir /r "$INSTDIR\apps\clock"	; ditto - this is replaced with the SnarlClock extension

  RMDir /r "$INSTDIR\docs"

;  RMDir /r "$INSTDIR\extensions"

  SetOutPath "$INSTDIR"
;  SetOverwrite ifnewer
  SetOverwrite On

  ; R2.04 installer included some guff it shouldn't have...
  Delete $INSTDIR\*.zip
  Delete $INSTDIR\*.rtf

  ; main application

  File /r "App\*.*"

  ; user guide

;  CreateDirectory "$INSTDIR\docs"
;  SetOutPath "$INSTDIR\docs"
;  File /r "docs\*.*"


  ; ----------------------------------------------------------------------
  ; 	Things that need registering
  ; ----------------------------------------------------------------------


  ; Type Libraries

  !insertmacro InstallLib TLB NOTSHARED REBOOT_PROTECTED "libs\style_engine.tlb" "$SYSDIR\style_engine.tlb" "$SYSDIR"
  !insertmacro InstallLib TLB NOTSHARED REBOOT_PROTECTED "libs\snarl_extensions.tlb" "$SYSDIR\snarl_extensions.tlb" "$SYSDIR"
  ;!insertmacro InstallLib TLB NOTSHARED REBOOT_PROTECTED "r4base.tlb" "$SYSDIR\r4base.tlb" "$SYSDIR"

  ; various new melon libraries - until the next release of melon is sorted...

  CreateDirectory "$INSTDIR\libs"

  ;!insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\web_resource.dll" "$INSTDIR\libs\web_resource.dll" "$SYSDIR"

  ; R2.4: snarl.library

  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\libsnarl.dll" "$SYSDIR\libsnarl.dll" "$SYSDIR"

  ; Install our standard style engines ($INSTDIR\styles will have been created above)

  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\style_engines\banner.dll" "$INSTDIR\styles\banner\banner.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\style_engines\ez.dll" "$INSTDIR\styles\ez\ez.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\style_engines\meter.dll" "$INSTDIR\styles\meter\meter.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\style_engines\prowl.dll" "$INSTDIR\styles\prowl\prowl.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\style_engines\mobile.dll" "$INSTDIR\styles\mobile\mobile.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\style_engines\runnable.dll" "$INSTDIR\styles\runnable\runnable.dll" "$SYSDIR"

  ; Install our standard extensions (folders should have been created by the "File /r ..." command)

  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\exts\AudioMon.dll" "$INSTDIR\extensions\AudioMon\AudioMon.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\exts\SnarlClock2.dll" "$INSTDIR\extensions\SnarlClock2\SnarlClock2.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\exts\SNPHTTP.dll" "$INSTDIR\extensions\SNPHTTP\SNPHTTP.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\exts\SysInfo.dll" "$INSTDIR\extensions\SysInfo\SysInfo.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\exts\TMinus.dll" "$INSTDIR\extensions\TMinus\TMinus.dll" "$SYSDIR"
  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\exts\wlanmonitor.dll" "$INSTDIR\extensions\wlan monitor\wlanmonitor.dll" "$SYSDIR"

  ; %app_data%

;  !insertmacro UAC.CallFunctionAsUser SetUserAppData

  Call SetUserAppData


  ; Associate *.sxz with sxzinst.exe

  WriteRegStr HKCR 'snarl_packed_extension\shell\Open\command' '' '"$INSTDIR\snarlm.exe" -ix "%1"'
  WriteRegStr HKCR '.sxz' '' 'snarl_packed_extension'
  ;${RefreshShellIcons}

SectionEnd

Section -AdditionalIcons

  SetOutPath $INSTDIR
;  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
;  CreateShortCut "$SMPROGRAMS\full phat products\Snarl\www.fullphat.net.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\full phat products\Snarl\Uninstall.lnk" "$INSTDIR\uninst.exe"

SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\Snarl.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\snarl.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Location" "$INSTDIR"
  WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Version" 0x00270055		; mmmmnnnn (39.85)

  ; delete the startup shortcut that 1.0 may have created...
;  Delete $SMSTARTUP\Snarl.lnk

SectionEnd


; Attempt to give the UAC plug-in a user process and an admin process.
Function .OnInit
 
UAC_Elevate:
    UAC::RunElevated 
    StrCmp 1223 $0 UAC_ElevationAborted ; UAC dialog aborted by user?
    StrCmp 0 $0 0 UAC_Err ; Error?
    StrCmp 1 $1 0 UAC_Success ;Are we the real deal or just the wrapper?
    Quit
 
UAC_Err:
    MessageBox mb_iconstop "A tree cannot flourish where no sunlight can reach."
    Abort
 
UAC_ElevationAborted:
    # elevation was aborted, run as normal?
    MessageBox mb_iconstop "The lark swirls in a dooomed attempt to regain composure."
    Abort
 
UAC_Success:
    StrCmp 1 $3 +4 ;Admin?
    StrCmp 3 $1 0 UAC_ElevationAborted ;Try again?
    MessageBox mb_iconstop "This installer requires admin access, try again"
    goto UAC_Elevate 
 
FunctionEnd


Function .OnInstFailed

    UAC::Unload ;Must call unload!

FunctionEnd

 
Function .OnInstSuccess

    UAC::Unload ;Must call unload!

FunctionEnd

;Function .onInstSuccess
;  ExecShell "open" "$INSTDIR\Alias"
;  MessageBox MB_ICONINFORMATION|MB_OK "Snarl was installed successfully and can be found in the Start Menu under 'full phat products'"
;  Exec notepad.exe ; view readme or whatever, if you want.
;  NoReadme:
;FunctionEnd


Function un.onUninstSuccess
;  HideWindow
;  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd


Function LaunchSnarl

  UAC::Exec "open" "$INSTDIR\snarl.exe" "" "" ""

;  ExecShell "open" "$SMPROGRAMS\full phat products\${PRODUCT_NAME}"
;  ExecShell "open" "$INSTDIR\snarl.exe"

FunctionEnd



Function checkforsnarl

  ReadRegStr $R0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Location"
  StrCmp $R0 "" skip 0

  ; key exists so we're doing a reinstall or upgrade - in which case we just need to kill Snarl...

  ExecWait '$R0\stop.exe'

skip:
  Call checkformelon

FunctionEnd


Function un.onInit

  IfSilent skip

  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to uninstall $(^Name)?" IDYES skip

  Abort

skip:

FunctionEnd







Section Uninstall

;  SetShellVarContext all

  ExecWait '"$INSTDIR\stop.exe"'

  ; Uninstall built-in extensions

  ; Uninstall built-in style engines


  ; Uninstall type library

  ; Uninstall temp melon 3.9 stuff

;  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\prefs_kit_d1s.dll" "$INSTDIR\libs\prefs_kit_d1s.dll" "$SYSDIR"
;  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\misc_resource_d0.dll" "$INSTDIR\libs\misc_resource_d0.dll" "$SYSDIR"
;  !insertmacro InstallLib REGDLL NOTSHARED REBOOT_PROTECTED "libs\libmgraphics2.dll" "$INSTDIR\libs\libmgraphics2.dll" "$SYSDIR"

  Call un.SetUserAppData

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"

SectionEnd




  ; ****************************************************
  ; ** Install APPDATA content
  ; ****************************************************

Function SetUserAppData

  ; Retrieving the path to user's Application Data folder.
  ; The result is automatically placed into $0 variable.
  UAC::GetShellFolderPath ${CSIDL_APPDATA} $APPDATA


  CreateDirectory "$0\full phat\snarl"
  CreateDirectory "$0\full phat\snarl\etc"
  CreateDirectory "$0\full phat\snarl\extensions"
  CreateDirectory "$0\full phat\snarl\styles"

  ; R2.4.2

  CreateDirectory "$0\full phat\snarl\styles\runfile"
  CreateDirectory "$0\full phat\snarl\styles\webforward"

  ; Extensions (NO LONGER INSTALLED - PRE-2.0 EXTENSIONS)

  SetOutPath "$0\full phat\snarl\extensions"
;  File /r "extensions\*.*"

  ; Links to extensions

  file /oname=AudioMon.extension tmp.link
  file /oname=SnarlClock2.extension tmp.link
  file /oname=SNPHTTP.extension tmp.link
  file /oname=SysInfo.extension tmp.link
  file /oname=TMinus.extension tmp.link
  file /oname=wlanmonitor.extension tmp.link

  Delete capslock.extension
  Delete donotdisturb.extension
  Delete ipmon.extension
  Delete snarlpower.extension


  ; Default exclude file

  SetOverwrite Off

  file libs\exclude

  SetOverwrite On

  ; User-specific style content

  SetOutPath "$0\full phat\snarl\styles"
  File /r "styles\*.*"

  ; Links to style engines

  file /oname=banner.styleengine tmp.link
  file /oname=ez.styleengine tmp.link
  file /oname=meter.styleengine tmp.link
  file /oname=prowl.styleengine tmp.link
  file /oname=mobile.styleengine tmp.link
  file /oname=runnable.styleengine tmp.link

;  this is for test/dev builds only!
;  file /oname=no_schemes.styleengine tmp.link


  ; Start Menu content

  UAC::GetShellFolderPath ${CSIDL_PROGRAMS} ""

;  SetShellVarContext all

  CreateDirectory "$0\full phat products\Snarl"
  CreateShortCut "$0\full phat products\Snarl\Snarl.lnk" "$INSTDIR\Snarl.exe"
  Delete "$0\full phat products\Snarl\Snarl App Manager.lnk"

  CreateShortCut "$0\full phat products\Snarl\Extras.lnk" "$INSTDIR\extras\"
  CreateShortCut "$0\full phat products\Snarl\Experimental Stuff.lnk" "$INSTDIR\experimental\"
  CreateShortCut "$0\full phat products\Snarl\Samples.lnk" "$INSTDIR\samples\"
  ; R2.4.1 - tools
  CreateShortCut "$0\full phat products\Snarl\Tools.lnk" "$INSTDIR\tools\"


  WriteIniStr "$INSTDIR\UserGuide.url" "InternetShortcut" "URL" "http://sourceforge.net/apps/mediawiki/snarlwin/index.php?title=User_Guide"
  CreateShortCut "$0\full phat products\Snarl\User Guide.lnk" "$INSTDIR\UserGuide.url" "" "$INSTDIR\user guide.ico"

  ; R2.4.2 - Runnable Styles Guide

  CreateShortCut "$0\full phat products\Snarl\Runnable Styles Guide.lnk" "$INSTDIR\etc\RunnableStylesGuide\index.url" "" "$INSTDIR\user guide.ico"



FunctionEnd


  ; ****************************************************
  ; ** Uninstaller: Remove APPDATA content
  ; ****************************************************

Function un.SetUserAppData

  ; Retrieving the path to user's Application Data folder.
  ; The result is automatically placed into $0 variable.

  ; Start menu content

;  Delete "$SMSTARTUP\Snarl.lnk"

  UAC::GetShellFolderPath ${CSIDL_PROGRAMS} ""

  RMDir /r "$0\full phat products\Snarl"
  RMDir "$0\full phat products"
  RMDir /r "$INSTDIR"

  ; AppData content

  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Do you want to remove Snarl's configuration settings as well?" IDNO skip
  UAC::GetShellFolderPath ${CSIDL_APPDATA} $APPDATA
  RMDir /r "$0\full phat\snarl"

skip:

FunctionEnd






